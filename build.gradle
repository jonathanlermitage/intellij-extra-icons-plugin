buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

group 'lermitage.intellij.extra.icons'
version version

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'test'
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    downloadSources Boolean.valueOf(downloadIdeaSources)
    instrumentCode Boolean.valueOf(instrumentPluginCode)
    pluginName 'Extra Icons'
    plugins = ['AngularJS']
    sandboxDirectory = "${rootProject.projectDir}/.idea-sandbox"
    updateSinceUntilBuild false
    version ideaVersion
}

repositories {
    mavenCentral()
}

// See https://github.com/ben-manes/gradle-versions-plugin
def versionIsNonStable = { String version ->
    if (['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().endsWith(qualifier) }) {
        return false
    }
    boolean isNonStable = ['alpha', 'Alpha', 'ALPHA', 'b', 'beta', 'Beta', 'BETA', 'rc', 'â€ŒRC', 'M', 'EA', 'pr', 'atlassian'].any { qualifier ->
        version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
    }
    return isNonStable
}
dependencyUpdates.gradleReleaseChannel = "current"
dependencyUpdates {
    rejectVersionIf {
        versionIsNonStable(it.candidate.version)
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
