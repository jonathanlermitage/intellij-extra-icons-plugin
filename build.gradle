buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            url 'https://jetbrains.bintray.com/intellij-plugin-service'
        }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.33.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.5.0'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij' // https://github.com/JetBrains/gradle-intellij-plugin
apply plugin: 'com.github.ben-manes.versions' // https://github.com/ben-manes/gradle-versions-plugin

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

group 'lermitage.intellij.extra.icons'
version version

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'test'
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    downloadSources Boolean.valueOf(downloadIdeaSources)
    instrumentCode Boolean.valueOf(instrumentPluginCode)
    pluginName 'Extra Icons'
    plugins = ['AngularJS']
    sandboxDirectory = "${rootProject.projectDir}/${ideaSandbox}-${ideaVersion}"
    updateSinceUntilBuild false
    version ideaVersion
}

buildSearchableOptions.enabled = Boolean.valueOf(enableBuildSearchableOptions)

runIde {
    jvmArgs '-Xms768m'
    jvmArgs '-Xmx2048m'
}

repositories {
    mavenCentral()
    mavenLocal()
}

// See https://github.com/ben-manes/gradle-versions-plugin
def versionIsNonStable = { String version ->
    if (['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().endsWith(qualifier) }) {
        return false
    }
    boolean isNonStable = ['alpha', 'Alpha', 'ALPHA', 'b', 'beta', 'Beta', 'BETA', 'rc', 'RC', 'M', 'EA', 'pr', 'atlassian'].any { qualifier ->
        version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
    }
    return isNonStable
}
dependencyUpdates.gradleReleaseChannel = "current"
dependencyUpdates {
    rejectVersionIf {
        versionIsNonStable(it.candidate.version)
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

test {
    useJUnitPlatform()
}
